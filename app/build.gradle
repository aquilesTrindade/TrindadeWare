def secrets = System.getenv("GOOGLE_SERVICES_JSON")
def analyticsEnabled = secrets != null

apply plugin: "com.android.application"
apply plugin: "kotlin-android"

if (analyticsEnabled) {
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
}

android {
    compileSdk 34

    def gitCommitHash = "git rev-parse HEAD".execute().text.trim()

    def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    
    def authorName = "git log -1 --pretty=format:'%an'".execute().text.trim()

    defaultConfig {
        multiDexEnabled true
        applicationId "com.trindade.ware"
        namespace "com.trindade.ware"
        //noinspection ExpiredTargetSdkVersion since we don't target getting Sketchware Pro on Google Play.
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 150
        versionName "TrindadeWare-" + authorName + getCommitHash

        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")
        
        buildConfigField("String", "COMMITER_NAME", "\"${authorName}\"")
        
        buildConfigField("String", "VERSION_NAME_WITHOUT_FLAVOR", "\"v1.0.0-${authorName}-${getCommitHash()}\"")

        buildConfigField("String", "CRASH_REPORT_WEBHOOK_URL", "\"${System.getenv("CRASH_REPORT_WEBHOOK_URL")}\"")
    }

    flavorDimensions = ["api"]

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        resources.excludes += "license/*"
        resources.excludes += "META-INF/DEPENDENCIES"
        resources.pickFirsts += "api_database/*"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    androidResources {
        additionalParameters "--stable-ids", "public-stable-ids.txt"
    }

    signingConfigs {
        debug {
            storeFile file("../testkey.keystore")
            storePassword "testkey"
            keyAlias "testkey"
            keyPassword "testkey"
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"], exclude: "proguard-base-7.2.2.jar")

    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.activity:activity:1.8.0"
    implementation "androidx.recyclerview:recyclerview:1.3.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.viewpager:viewpager:1.0.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.google.android.material:material:1.10.0"
    implementation "com.airbnb.android:lottie:3.1.0"
    implementation "com.google.android.flexbox:flexbox:3.0.0"

    implementation "androidx.annotation:annotation:1.6.0"
    implementation "com.android.tools:annotations:31.0.2"
    implementation "com.google.guava:guava:32.0.1-jre"
    implementation 'androidx.exifinterface:exifinterface:1.3.6'

    def editorGroupId = "io.github.Rosemoe.sora-editor"
    implementation platform("$editorGroupId:bom:0.21.1-f243159-SNAPSHOT")
    implementation "$editorGroupId:editor"
    implementation "$editorGroupId:language-java"
    implementation "$editorGroupId:language-textmate"

    implementation "de.hdodenhof:circleimageview:3.1.0"
    implementation "com.github.angads25:filepicker:1.1.1"

    implementation "com.android.tools.build:bundletool:1.15.1"
    //noinspection GradleDependency copied from above dependency for not just runtime access
    implementation "com.google.protobuf:protobuf-java:3.19.2"
    //noinspection GradleDependency
    implementation "com.android.tools:sdklib:25.3.0", { exclude group: "com.intellij", module: "annotations" }
    implementation "com.android.tools:r8:8.0.40"
    implementation "com.github.Iyxan23:zipalign-java:1.1.2"

    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.madgag:scpkix-jdk15on:1.47.0.2"

    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    //noinspection GradleDependency
    implementation "com.squareup.okio:okio:3.1.0"
    implementation "com.github.Cosmic-Ide:DependencyResolver:868996895a"

    implementation "net.sf.proguard:proguard-base:6.0.3"
    implementation files('libs/proguard-base-7.2.2.jar')
    implementation 'com.guardsquare:proguard-core:9.0.2'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22'

    //noinspection GradleDependency
    implementation "org.eclipse.jdt:ecj:3.13.0"
    implementation "org.eclipse.jdt:ecj:3.26.0"

    implementation project(path: ":kotlinc")

    implementation "com.github.topjohnwu.libsu:core:5.0.4"
    implementation "dev.chrisbanes.insetter:insetter:0.6.1"

    // Analytics and Crashlytics
    if (analyticsEnabled) {
        new File("$projectDir/google-services.json").text = secrets
    }
    implementation platform("com.google.firebase:firebase-bom:31.2.1")
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"
    
    // New FilePicker 
    implementation "com.github.Ruan625Br:FilePickerSphere:1.0.0"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs_nio:2.0.3"
}
